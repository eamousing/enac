module enac_links

    use enac_commons
    use enac_functions

    implicit none

contains

    subroutine update_parameters()
        !! Updates the true SI parameters for all SIs that are sensitive to env. drivers,
        !! by calling the link functions. Link functions generate parmult, 
        !! which are multipliers to siparam.
        !! The parameters in the link functions are in link_parameters
        !! Called for each species and time step by enac_main.f90
        !!
        !! Update_parameters uses true environment values and updates siparam 
        !! man_obs calls linkfunc with corrupted values and updates desparam
        !!
        !! NOTE: The number of link-functions and the max. number of parameters per link 
        !! function is hard-coded in the enac_commons module. 
        !! Additional link functions will have to be coded.
        real :: parmult(maxspec, maxfunc, maxfuncparam) !! Parameter multipliers generated by the link-functions
        integer :: i,j,k,nlink,kilde
        real :: decp(maxspec, 3)
   
        kilde=1 ! kilde= 1 signals that we are dealing with true values
        parmult(species, :, :) = 1.0
    
        ! Parameter multipliers from link-functions, but not in the priming phase
        ! When allocated to si's in newsis the working values (..2) are set equal to 
        ! the standard values (..1) and these remain in the priming phase
        if (year .gt. 0) then
            if (nlinkfunc(species) .gt. 0) then
                do nlink = 1, nlinkfunc(species)
                    ! NOTE: n-link=0 is reserved for recruitment
                    call linkfunc(nlink, parmult, kilde, decp)
                enddo
            endif
        
            ! Update individual SIparmeters with these multipliers  
            do i = nsistart(species), nsi2(species)
                do j = 1, maxfunc
                    do k = 1, maxfuncparam
                        siparam(species,i,j,k,2) = siparam(species,i,j,k,1) * parmult(species,j,k)
                    enddo
                enddo
            enddo
        
            ! If recruiting season, link function for recruitment
            if (icodeshift(species) .eq. 3) call r_link_test()
            if (link_parameters(species, 3, 1) .eq. 1 .and. species .eq. 3) call mac_pred()
        endif
    end subroutine update_parameters

    subroutine decis_param()
        !! decis calls linkfunc with corrupted values and updates desparam
        !! Sets up desparam according to varenv for the current species, year and season
        !! The call to linkfunct with option 2 will put the result in desparam
        real :: parmult(maxspec,maxfunc,maxfuncparam) !! Parameter multipliers generated by the link-functions
        integer :: kilde, j, k, nlink
        real :: decp(maxspec, 3)
      
        parmult(species,:,:) = 1.0
        kilde = 2
        ! Kilde=2 signals that we are dealing with managers values
        if (year .gt. 0) then
            if (nlinkfunc(species) .gt. 0) then
                do nlink = 1, nlinkfunc(species)
                    call linkfunc(nlink, parmult, kilde, decp)
                enddo
            endif
          
            ! Update parmeters for decision with these multipliers
            do j = 1, maxfunc
                do k = 1, maxfuncparam
                    decparam(species,j,k) = stdparameters(species,j,k,1) * parmult(species,j,k)
                    if (j .eq. 1 .and. k .eq. 2) decparam(species,j,k) = decp(species,2) ! Adult K
                    if (j .eq. 1 .and. k .eq. 3) decparam(species,j,k) = decp(species,3) ! Varians
                    if (j .eq. 1 .and. k .eq. 4) decparam(species,j,k) = decp(species,1) ! Juvenile K
                enddo
            enddo
            ! If recruiting season, link function for recruitment
            if (season .eq. recruit_season(species)) call r_link(kilde)
        endif
    end subroutine decis_param

    ! ***************** LINKFUNCTIONS ************************
    ! For the time being, the link-functions are not used. 
    ! That is achieved by hard-coding all environment variables to
    ! unknown in subroutine envinput

    subroutine linkfunc(nlink,parmult,kilde,decp)
        !! Updates the parameters specified in the link function nlink
        !! This subroutine is just a central for calling specific subroutines
        !! It gets values in paramult, which are multipliers for the 
        !! population parameters. siparam = sistandard*paramult in subroutine 
        !! update_parameters
        integer :: nlink
        integer, intent(in) :: kilde
        ! parmult is parameter multipliers generated by the link-functions
        real :: parmult(maxspec,maxfunc,maxfuncparam)
        real :: ktemp, condtemp, decp(maxspec, 3)
    
        if (nlink .eq. 1) then
            call linkfunction_1(decp) 
            ktemp = 1.
            parmult(species,1,2)=ktemp
        else if (nlink .eq. 2) then
            !call linkfunction_2(condtemp,kilde)
            condtemp = 1
            parmult(species,2,1)=condtemp
        else
            write(*,*) 'Called for non-existing link function',nlink,kilde
        stop
        endif
    end subroutine linkfunc

    subroutine r_link(kilde)
        integer, intent(in) :: kilde
        real :: a1, a2, p, x, x0, r0
        integer k1, regime
        integer :: i
        
        k1 = kilde + 1
      
        do i = 1, maxspec
            ! Link function number 1: Recruitment as function of temperature
            ! Recruitment functions are special, because they update only R0, 
            ! which goes into the common variable rmult in the calling function
            x0 = link_parameters(i,1,1)
            a1 = link_parameters(i,1,2)
            a2 = link_parameters(i,1,3)
            p = link_parameters(i,1,4)
            regime = nregact(i,year)
            ! x is distance from temperature optimum, varenv gives the temperature as
            ! variable number 1 in year iy and season mnd
            if (varenv(year,season,1,kilde) .gt. -999.0) then
                ! no modification if varenv is missing, assume optimal conditions
                x = varenv(year,season,1,kilde) - x0
            else
                x = 0.0
            endif
            call assymbell(a1,a2,p,x,r0)
            recruit_parameters(i,regime,4,k1) = r0 * recruit_parameters(i,regime,4,1)
        enddo
    end subroutine r_link

    subroutine linkfunction_1(decp)
        ! Growth multipliers as a function of total biomass fish 
        real :: decp(maxspec,3) ! Intend?
        integer :: si, month, i, species_save
        real :: Lu_a, Lu_b, K_a, K_b, K_a_juv, K_b_juv, alfa
        real :: jsb, biomass(3), var, k, s, b1, b2
        real :: eps, sigma, eps1, sigma1 
      
        if(season.eq.1) then 
            Lu_a = link_parameters(species,1,1)
            Lu_b = link_parameters(species,1,2)
            K_a  = link_parameters(species,1,3)
            K_b  = link_parameters(species,1,4)
            var  = link_parameters(species,1,5)  
            K_a_juv = link_parameters(species,1,6)
            K_b_juv = link_parameters(species,1,7)
            alfa = link_parameters(species,1,8)
        
            species_save = species
            biomass = 0.0
            do i = 1, 3
                species = i
                call get_tsb(s)
                if (i .eq. 3) call get_ssb(s) 
                biomass(i) = s / 10**6
            end do
            
            species = species_save 
            
            b1 = 0.0
            jsb = 0.0
            
            ! Set the correct startweight at age 1 for herring and calculate covariates 
            if (species .eq. 3) then
                do si = nsistart(species), nsi2(species)
                    if (sistate(species,si,2) .eq. 1) then
                        !sistate(species,si,4) = 12 !0.012
                        b1 = b1 + (sistate(species,si,1) * sistate(species,si,4))
                    end if
                    if(sistate(species,si,2) .lt. 5) then
                        !jsb = jsb + (sistate(species,si,1)*(sistate(species,si,4)*0.001))
                        jsb = jsb + (sistate(species,si,1)*sistate(species,si,4))
                    end if
                end do
                jsb = jsb / 100000000.0
            end if
      
            do si = nsistart(species), nsi2(species)
      
                !Hvis sild  
                if (species .eq. 3) then    
                    if (sistate(species,si,2) .eq. 1) then
                        !eps=exp(var)**0.5 
                        eps = 0.1719
                        sigma=rand_normal(0.,eps)
                        !b2 = log(b1/1000)
                        b2 = log(b1)
                        !Length at age 1 is calculated below
                        sistate(species,si,3) = exp((2.40326 + (eps**2 * 0.5)) + &
                            (-0.0130049 * b2) + sigma) 
                        ! Lend skal sette n�r silden er 1 �r gammel
                        siparam(species,si,1,1,2) = exp(Lu_a + Lu_b * sistate(species,si,3))
                    end if
                end if
         
                eps1 = exp(var)**0.5
                sigma1 = rand_normal(0.,eps1)
                !sigma1=0.
      
                if (species .eq. 3 .and. sistate(species,si,2) .lt. 5) then
                    siparam(species,si,1,2,2) = exp(K_a_juv + K_b_juv*log(jsb)+sigma1) 
                else if (species .eq. 1) then !blue whiting
                    siparam(species,si,1,2,2) = exp(K_a + K_b*biomass(1))
                else if (species .eq. 2) then !mackerel
                    siparam(species,si,1,2,2) = exp(K_a + K_b*sum(biomass(2:3)))
                else
                    siparam(species,si,1,2,2) = exp((K_a + K_b*log(biomass(species)))+sigma1)
                    !if(si==nsistart(species))print *, siparam(species,si,1,2,2),log(biomass) 
                end if
                
                siparam(species,si,1,3,2) = var
      
                !for decmodul (decparam) - ERROR AT THIS LINE! John 07.10.2021
                if(species .eq. 3) decp(species,1) = exp(K_a_juv + K_b_juv*log(jsb)) !juvenil K
                if(species .ne. 3) decp(species,1) = exp(K_a_juv + K_b_juv*jsb) !juvenil K
                if(species .eq. 2) decp(species,2) = exp(K_a + K_b*sum(biomass(2:3)))     !voksen K  
                if(species .eq. 1) decp(species,2) = exp(K_a + K_b*biomass(1))
                if(species .eq. 3) decp(species,2) = exp(K_a + K_b*log(biomass(species)))     !voksen K  
                decp(species,3) =  var                             !var 
            end do 
        end if !season eq 1
    end subroutine linkfunction_1

    subroutine linkfunction_2(condtemp,kilde)
        ! Condition multipliers as a function of temperature and plankton availability
        ! The multipliers are transferred to paramult, that are multipliers on
        ! sistandard into siparam in subroutine update_parameters
        integer :: c_season,iiy
        real :: x0,a1,a2,p,xc,ctemp1,prabu,p50,slope,v,minc
        integer, intent(in) :: kilde
        real, intent (out) :: condtemp
        real :: ctemp
      
        ! Step 1: condition as function of environment (assymbell)
        ! x0 is temperature optimum
        x0 = link_parameters(species,2,8)
        a1 = link_parameters(species,2,10)
        a2 = link_parameters(species,2,11)
        p = link_parameters(species,2,12)
            
        ! Here, we assume the environment variable some time ago
        ! It can be in the season indicated by c_season=link_parameters(species,3,8)
        ! or it can be c-seasontime steps ago. The latter case is indicated by a negative number
        ! c_season = 0 means the current season
        ! Also, there is some cheating in the first year, 
        ! if the reference time is before the start of the series
        c_season = nint(link_parameters(species,2,8)) 
        if (c_season .le. 0 .and. c_season .ne. -999 ) then
            c_season = season+c_season
            iiy=year
            if (c_season .lt. 1) then
                c_season = c_season + ntimestep
                iiy = iiy - 1
                if (iiy .lt. 1) iiy = 1
            end if 
        else
            c_season = season
            iiy = year
        end if
         
        if (iiy .gt. 0) then
            if (varenv(iiy,c_season,2,kilde) .gt. -999.0) then
                xc = varenv(iiy,c_season,2,kilde) - x0
                call assymbell(a1, a2, p, xc, ctemp1)
                ! Step 2: condition modified by prey abundance (logistic)
                prabu = varenv(year, season, 3, kilde)
                p50 = link_parameters(species,3,6)
                slope = link_parameters(species,3,7)
                call logistic(p50, slope, prabu, v)
                ! Final condition
                minc = link_parameters(species,2,1)
                ctemp = ctemp1*v*(1.0-minc)+minc
            else
                ! case condition determined by environment before start of environment series
                condtemp=1.0
            endif
        else
            ! Varenv missing: condtemp=1
            condtemp=1.0
        endif
    end subroutine linkfunction_2

    ! ---------------- Test ny linkfunc for rekruttering

    subroutine r_link_test()
        ! integer	::	i,species_save
        ! real		:: 	s,macbio
        ! species_save = species
        ! species = 2
        ! call get_ssb(s)
        ! macbio=s/1000000
        ! species = species_save
        
        if (species .eq. 3) then !if herring
            !Climate effect
            if(varenv1(2,year,1).gt.21.5) then
                recregime(species) = 3
            else if(varenv1(2,year,1).lt.21.3) then
                recregime(species) = 1
            else
                recregime(species) = 2
            endif
        endif
    end subroutine r_link_test 

    subroutine mac_pred
        ! This is updated mac predation routine that calculates a new SD/sigma (effect) as 
        ! a function of mackerel predation. The linear parameters are estimated from a 
        ! 90th quantile regression on recruitment residuals (fitted from Ricker) vs mackerel abundance
        ! integer	::	i
        ! real		:: 	s
        integer :: species_save
        integer :: si

        species_save = species
        macnum=0.
        do si = nsistart(2), nsi2(2)
           if (sistate(2,si,2) .ge. 2) macnum = macnum + sistate(2,si,1)
        enddo
     
        if (species .eq. 3) then !if herring
           ! Predation effect; 1.28 is the standard normal value at the 90th percentile
           effect(species) = (-5.826e-8 * macnum + 1.976) / 1.28
        else
           effect(species) = 1.0
        end if
    end subroutine mac_pred

end module enac_links